; WORDLE-like game for 6502 emulator
; (http://6502.cdot.systems)
;
; Chris Tyler 2022-09-21 for use in the SPO600 course.
; (C)2022 Seneca College of Applied Arts and Technology
;
; Licensed under the terms of the GNU General Public License
; (GPL) version 2 or higher. See COPYING file.

; ROM routines
define		SCINIT		$ff81 	; initialize/clear screen
define		CHRIN		$ffcf 	; input keyboard character
define		CHROUT		$ffd2 	; output character to screen
define		SCREEN		$ffed 	; get screen size
define		PLOT		$fff0 	; get/set cursor coordinates

; constants
define		MAXIN		5     	; maximum chars in input
define		SPACE		$20   	; white space symbol
define		CURSOR		$A0   	; black space symbol
define		CURSOR_LEFT	$83   	; move cursor left
define		BACKSPACE	$08   	; backspace character
define		ENTER		$0d   	; ENTER character

define	COLOUR_BLACK		$00	; graphics display colour codes
define	COLOUR_WHITE		$01
define	COLOUR_RED		$02
define	COLOUR_CYAN		$03
define	COLOUR_PURPLE		$04
define	COLOUR_GREEN		$05
define	COLOUR_BLUE		$06
define	COLOUR_YELLOW		$07
define	COLOUR_ORANGE		$08
define	COLOUR_BROWN		$09
define	COLOUR_LIGHT_RED	$0A
define	COLOUR_DARK_GREY	$0B
define	COLOUR_DARK_GRAY	$0B
define	COLOUR_GREY		$0C
define	COLOUR_GRAY		$0C
define	COLOUR_LIGHT_GREEN	$0D
define	COLOUR_LIGHT_BLUE	$0E
define	COLOUR_LIGHT_GREY	$0F
define	COLOUR_LIGHT_GRAY	$0F

define	COLOR_BLACK		$00	; american spellings :-/
define	COLOR_WHITE		$01
define	COLOR_RED		$02
define	COLOR_CYAN		$03
define	COLOR_PURPLE		$04
define	COLOR_GREEN		$05
define	COLOR_BLUE		$06
define	COLOR_YELLOW		$07
define	COLOR_ORANGE		$08
define	COLOR_BROWN		$09
define	COLOR_LIGHT_RED		$0A
define	COLOR_DARK_GREY		$0B
define	COLOR_DARK_GRAY		$0B
define	COLOR_GREY		$0C
define	COLOR_GRAY		$0C
define	COLOR_LIGHT_GREEN	$0D
define	COLOR_LIGHT_BLUE	$0E
define	COLOR_LIGHT_GREY	$0F
define	COLOR_LIGHT_GRAY	$0F

; zero-page locations
define	GUESS_COUNT		$10	; number of guesses so far
define	GREEN_COUNT		$11	; number of green dots
define	CHAR_NUMBER		$12	; temporary for character number
define	WORD_POINTER		$13	; pointer to selected random word
define	WORD_POINTER_HI		$14	

; other locations in memory
define	GUESS_TEXT		$2000   ; 5 characters
define	ANSWER_BUFFER		$2010	; copy of correct answer
define	ANSWER			$2020	; correct answer

	JSR SCINIT	; initialize screen

NEW_GAME:
	LDA #$00
	STA WORD_POINTER_HI

	LDA $FE	; get a random byte value
	STA WORD_POINTER

	; convert random byte to a random word by
	; multiplying it by 5 (chars/word) and adding
	; the base address of the word list
	ASL		; x2
	ROL WORD_POINTER_HI
	ASL		; x4
	ROL WORD_POINTER_HI
	CLC
	ADC WORD_POINTER ; +1 = x5#$00
	STA WORD_POINTER
	LDA WORD_POINTER_HI
	ADC #$00
	STA WORD_POINTER_HI

	LDA WORD_POINTER	; add memory offset to word list
	CLC
	ADC #<WORD_LIST		; ... this is the low byte of word list address
	STA WORD_POINTER
	LDA WORD_POINTER_HI
	ADC #>WORD_LIST		; ... this is the high byte of word list address
	STA WORD_POINTER_HI

	; copy the selected word to the ANSWER buffer
	LDY #$00
COPY_ANSWER_CHAR:
	LDA (WORD_POINTER),Y
	STA ANSWER,Y
;	JSR CHROUT		; uncomment to show word (debugging)
	INY
	CPY #MAXIN
	BNE COPY_ANSWER_CHAR
;	LDA #$0D		; uncomment to show word (debugging)
;	JSR CHROUT		; uncomment to show word (debugging)

	LDA #COLOUR_WHITE	; clear bitmap to white
	JSR BM_CLEAR

	LDA #$00		; initialize guess number to zero
	STA GUESS_COUNT
	
; ---------------------------------------------------------------

GET_GUESS:
	JSR PRINT
	dcb "E","n","t","e","r",32,"y","o","u","r",32
	dcb "g","u","e","s","s",":",32,00

	JSR GETINPUT		; get guess from the user
	
	; make a copy of the answer (so we can zero out used chars)
	; ANSWER_BUFFER will contain $FF for any "green" character
	; ANSWER_BUFFER will the ascii code for any other character
	; ... this will get replaced with 0 if the character is used
	;     for a "yellow" match (correct character in wrong position)                     
	LDX #$00
BUFFER_COPY_CHAR:
	LDA ANSWER,X
	CMP GUESS_TEXT,X
	BNE NOT_GREEN
	LDA #$FF

NOT_GREEN:
	STA ANSWER_BUFFER,X
	INX
	CPX #MAXIN
	BNE BUFFER_COPY_CHAR
	
	; score the user's guess
	LDY GUESS_COUNT		; row number is same as guess number
	LDX #$00		; column number = character = 0
	STX GREEN_COUNT		; count of green boxes

CHECK_CHAR:
	LDA GUESS_TEXT,X	; get char X of guess_text
	LDA ANSWER_BUFFER,X	; check char X of answer
	CMP #$FF		; see if it's already marked as a match
	BEQ GREEN_DOT		; ... match, draw green dot

	
	STX CHAR_NUMBER		; save the character number
	LDA GUESS_TEXT,X	; get char X of guess_text
	LDX #$00		; load X with 0

YELLOW_CHECK:
	CMP ANSWER_BUFFER,X	; compare each char
	BEQ YELLOW_DOT		; ... if match draw yellow dot
	INX
	CPX #MAXIN		; see if we've processed all chars
	BNE YELLOW_CHECK	; ... check next char if not

	LDX CHAR_NUMBER		; restore character number
	LDA #COLOUR_BLACK	; draw black dot
	JMP DRAW_DOT

YELLOW_DOT:
	LDA #$00		; zero out character from answer
	STA ANSWER_BUFFER,X

	LDX CHAR_NUMBER		; restore character number

	LDA #COLOUR_YELLOW	; draw yellow dot
	JMP DRAW_DOT

GREEN_DOT:
	INC GREEN_COUNT

	LDA #$00		; zero out character from answer
	STA ANSWER_BUFFER,X

	LDA #COLOUR_GREEN	; draw green dot

DRAW_DOT:
	JSR BM_BIGDOT

	INX
	CPX #MAXIN	; check to see if we've processed all chars
	BNE CHECK_CHAR	; ... process nexts char if not

	LDA GREEN_COUNT	; see if last guess was 5 green boxes
	CMP #5
	BEQ WIN		; ... if do print win message

	INC GUESS_COUNT	; increment guess number
	LDA #6		; max number of guesses
	CMP GUESS_COUNT	; have we hit max?
	BNE GET_GUESS	; if not, get another guess

	JSR PRINT	; all guesses used, print lose message
	dcb "Y","o","u",32,"l","o","s","e",".",$0D,$0D,$00
	JMP PLAY_AGAIN

WIN:
	JSR PRINT
	dcb "Y","o","u",32,"w","i","n","!",$0D,$0D,$00

PLAY_AGAIN:		; print prompt and get keystroke before erasing bitmap
	JSR PRINT
	dcb "-","-","-","-","-","-","-",$0d
	dcb "P","r","e","s","s",32,"E","N","T","E","R",32
	dcb "f","o","r",32,"a",32,"n","e","w",32
	dcb "g","a","m","e",".",".",".",$0d
	dcb 00

WAIT_KEY:
	JSR CHRIN
	CMP #$0D
	BNE WAIT_KEY

	JMP NEW_GAME

; ==============================================================

GETINPUT:
	LDY #$00	; count of input characters

CURSOR_INPUT:
	LDA #CURSOR	; draw cursor block and move on top of it
	JSR CHROUT
	LDA #CURSOR_LEFT
	JSR CHROUT

GETCHAR:
	JSR CHRIN	; get a character
	CMP #$00	; compare with null
	BEQ GETCHAR	; ... get another char if null

	CMP #BACKSPACE	; compare with backspace
	BNE NOT_BACKSPACE	; ... skip ahead if not

	CPY #$00	; do we have any characters?
	BEQ GETCHAR	; ... get another char if not (can't backspace)

	DEY		; reduce character count by one
	LDA #SPACE	; draw space character to erase
	JSR CHROUT
	LDA #CURSOR_LEFT
	JSR CHROUT	; move left to the erased character
	JSR CHROUT	; move left to before the erased character
	JMP CURSOR_INPUT

NOT_BACKSPACE:
	CMP #ENTER	; compare with ENTER
	BNE NOT_ENTER	; ... continue if not
	CPY #MAXIN	; see if we have MAXIN characters
	BNE NOT_ENTER	; ... continue if not (ENTER not allowed)

	LDA #SPACE	; erase cursor
	JSR CHROUT
	LDA #ENTER	; move to next line
	JSR CHROUT
	RTS		; return -- data is in the buffer

NOT_ENTER:
	CMP #97		; letter 'a'
	BCC CHECK_UPPER	; ... if code is lower, check for uppercase
	CMP #123	; letter 'z' + 1
	BCS GETCHAR	; ... if code is greater then forget it
	CLC
	SBC #31		; subtract 31 to convert to uppercase

CHECK_UPPER:
	CMP #65		; letter 'A'
	BCC GETCHAR	; ... get another char if less
	CMP #91		; letter 'Z' plus one
	BCS GETCHAR	; ... get another char if > Z
	CPY #MAXIN	; do we have max # chars?
	BCS GETCHAR	; ... ignore and get another char if so

	STA GUESS_TEXT,Y	; add character to input string
	INY
	JSR CHROUT	; print the character

	JMP CURSOR_INPUT

; ================================================

BM_CLEAR:	; clear the bitmapped display
		; A contains colour code to be used
	LDY #$00
BM_CLEAR_LOOP:
	STA $0200,Y
	STA $0300,Y
	STA $0400,Y
	STA $0500,Y
	INY
	BNE BM_CLEAR_LOOP
	RTS

; ================================================
; zero-page locations used by this routine
define	BM_PLOT_A		$00
define	BM_PLOT_X		$01
define	BM_PLOT_Y		$02
define	BM_POINTER_LO		$03
define	BM_POINTER_HI		$04

BM_PLOT:	; bitmap plot a pixel
		; A contains colour code
		; Y is Y-coordinate (0 at top)
		; X is X-coordinate (0 at left)

	STA BM_PLOT_A	; save register values
	STX BM_PLOT_X
	STY BM_PLOT_Y	

	; multiply row number by 32 (pixels/row)
	LDA #$00		; zero out the high byte
	STA BM_POINTER_HI

	STY BM_POINTER_LO	; store the Y value to the low byte

	ASL BM_POINTER_LO	; *2
	ASL BM_POINTER_LO	; *4
	ASL BM_POINTER_LO	; *8
	ASL BM_POINTER_LO	; *16 with carry
	ROL BM_POINTER_HI
	ASL BM_POINTER_LO	; *32 with carry
	ROL BM_POINTER_HI

	; now add the address of the bitmap
	LDA #$02	; page number: bitmap start
	CLC
	ADC BM_POINTER_HI
	STA BM_POINTER_HI

	; restore the colour code
	LDA BM_PLOT_A

	; get column number into Y
	LDY BM_PLOT_X

	; store the colour code
	STA (BM_POINTER_LO),Y

	; restore the registers
	LDY BM_PLOT_Y
	LDX BM_PLOT_X

	; return
	RTS

; ================================================

; zero-page locations used by this routine
define	BM_BIGDOT_A		$05
define	BM_BIGDOT_X		$06
define	BM_BIGDOT_Y		$07
define	BM_BIGDOT_X5		$08
define	BM_BIGDOT_HEIGHT	$09

BM_BIGDOT:	; bitmap draw a big wordle dot (4x4 px)
		; A is the colour code
		; Y is the row (0-5)
		; X is the column (0-4)

	STA BM_BIGDOT_A	; save registers 
	STX BM_BIGDOT_X
	STY BM_BIGDOT_Y

	; dots are 4x4 pixels with one pixel
	; between each dot -- therefore multiply
	; X and Y values by 5 to find upper-left
	; corner of the dot

	TYA
	ASL	; x2
	ASL	; x4
	CLC	; add one more to get x5
	ADC BM_BIGDOT_Y
	TAY

	TXA
	ASL	; x2
	ASL	; x4
	CLC	; add one more to get x5
	ADC BM_BIGDOT_X
	TAX
	STA BM_BIGDOT_X5

	; set up a decrementing counter for rows
	LDA #$04	; height/width in pixels
	STA BM_BIGDOT_HEIGHT

BM_BIGDOT_ROW:
	LDX BM_BIGDOT_X5	; reset x coordinate
	LDA BM_BIGDOT_A		; load colour code
	JSR BM_PLOT		; plot point
	INX			; repeat across the row (4px)
	JSR BM_PLOT
	INX
	JSR BM_PLOT
	INX
	JSR BM_PLOT
	INY			; increment row number
	DEC BM_BIGDOT_HEIGHT	; decrement height counter
	BNE BM_BIGDOT_ROW	; ... continue if not done

	LDA BM_BIGDOT_A	; restore registers
	LDX BM_BIGDOT_X
	LDY BM_BIGDOT_Y

	RTS

; ==============================================================

; print the null-terminated string immediately after the 
; "JSR PRINT"

; zeropage locations used in this routine
define		PRINT_PTR	$20
define		PRINT_PTR_H	$21

PRINT:		pla
		clc
		adc #$01
		sta PRINT_PTR
		pla
		sta PRINT_PTR_H

		tya
		pha

		ldy #$00
print_next:	lda (PRINT_PTR),y
		beq print_done
		
		jsr CHROUT
		iny
		jmp print_next

print_done:	tya
		clc
		adc PRINT_PTR
		sta PRINT_PTR

		lda PRINT_PTR_H
		adc #$00
		sta PRINT_PTR_H

		pla
		tay

		lda PRINT_PTR_H
		pha
		lda PRINT_PTR
		pha

		rts
PRINT:		PLA
		CLC
		ADC #$01
		STA PRINT_PTR
		PLA
		STA PRINT_PTR_H

		TYA
		PHA

		LDY #$00
PRINT_NEXT:	LDA (PRINT_PTR),Y
		BEQ PRINT_DONE
		
		JSR CHROUT
		INY
		JMP PRINT_NEXT

PRINT_DONE:	TYA
		CLC
		ADC PRINT_PTR
		STA PRINT_PTR

		LDA PRINT_PTR_H
		ADC #$00
		STA PRINT_PTR_H

		PLA
		TAY

		LDA PRINT_PTR_H
		PHA
		LDA PRINT_PTR
		PHA

		RTS

; =======================================================

; list of 256 5-letter words
WORD_LIST:
dcb "T","I","T","L","E"
dcb "C","R","O","S","S"
dcb "E","X","I","S","T"
dcb "S","T","O","C","K"
dcb "O","T","H","E","R"
dcb "N","O","V","E","L"
dcb "B","R","I","N","G"
dcb "W","H","I","L","E"
dcb "A","B","O","U","T"
dcb "A","W","A","R","E"
dcb "P","R","O","O","F"
dcb "P","I","T","C","H"
dcb "G","L","A","S","S"
dcb "C","H","I","E","F"
dcb "B","R","E","A","K"
dcb "U","R","B","A","N"
dcb "P","L","A","I","N"
dcb "O","C","C","U","R"
dcb "R","O","U","N","D"
dcb "N","E","V","E","R"
dcb "B","L","A","M","E"
dcb "T","H","I","N","K"
dcb "B","L","O","O","D"
dcb "Q","U","E","E","R"
dcb "P","L","U","M","B"
dcb "S","H","A","P","E"
dcb "C","R","I","M","E"
dcb "N","I","G","H","T"
dcb "T","R","A","D","E"
dcb "B","O","A","R","D"
dcb "T","O","T","A","L"
dcb "S","T","O","U","R"
dcb "T","A","S","T","E"
dcb "B","L","I","N","D"
dcb "G","R","O","U","P"
dcb "F","I","T","L","Y"
dcb "A","F","O","U","L"
dcb "V","I","S","I","T"
dcb "S","T","E","E","P"
dcb "U","S","U","A","L"
dcb "F","R","A","N","K"
dcb "A","B","U","S","E"
dcb "P","I","E","C","E"
dcb "F","U","R","T","H"
dcb "R","I","G","H","T"
dcb "C","H","I","N","A"
dcb "M","I","N","O","R"
dcb "B","A","S","I","S"
dcb "A","G","A","P","E"
dcb "C","H","I","L","D"
dcb "B","A","L","L","Y"
dcb "F","A","U","L","T"
dcb "S","P","I","T","E"
dcb "T","H","A","N","K"
dcb "A","H","E","A","D"
dcb "B","E","L","O","W"
dcb "J","O","I","N","T"
dcb "S","I","X","T","H"
dcb "R","A","T","I","O"
dcb "R","A","D","I","O"
dcb "M","I","N","U","S"
dcb "F","I","G","H","T"
dcb "B","A","S","I","C"
dcb "P","O","U","N","D"
dcb "S","P","L","I","T"
dcb "C","H","E","C","K"
dcb "R","I","V","E","R"
dcb "S","T","A","R","T"
dcb "G","R","E","E","N"
dcb "S","I","G","H","T"
dcb "I","N","D","E","X"
dcb "W","A","T","C","H"
dcb "H","O","U","S","E"
dcb "S","C","O","P","E"
dcb "B","U","R","S","T"
dcb "S","O","L","V","E"
dcb "S","I","N","C","E"
dcb "F","R","U","I","T"
dcb "A","S","K","E","W"
dcb "G","A","Y","L","Y"
dcb "K","N","I","F","E"
dcb "S","P","A","N","G"
dcb "B","R","E","A","D"
dcb "S","I","M","O","N"
dcb "S","U","G","A","R"
dcb "E","A","R","T","H"
dcb "S","T","U","F","F"
dcb "T","H","R","O","W"
dcb "T","O","W","E","R"
dcb "R","O","M","A","N"
dcb "S","Q","U","A","D"
dcb "A","F","O","R","E"
dcb "C","L","A","S","S"
dcb "W","O","M","A","N"
dcb "A","L","O","F","T"
dcb "B","R","O","W","N"
dcb "A","B","A","F","T"
dcb "W","H","I","C","H"
dcb "T","H","I","C","K"
dcb "H","O","T","E","L"
dcb "G","U","E","S","S"
dcb "S","P","E","E","D"
dcb "S","H","O","C","K"
dcb "P","A","P","E","R"
dcb "I","N","F","R","A"
dcb "P","R","O","V","E"
dcb "G","O","D","L","Y"
dcb "S","U","P","E","R"
dcb "L","O","W","L","Y"
dcb "T","O","U","G","H"
dcb "V","A","L","U","E"
dcb "C","A","R","R","Y"
dcb "W","H","I","T","E"
dcb "M","O","D","E","L"
dcb "S","H","E","E","R"
dcb "T","O","M","O","Z"
dcb "M","A","D","L","Y"
dcb "W","E","T","L","Y"
dcb "M","O","R","A","L"
dcb "R","O","Y","A","L"
dcb "S","P","E","A","K"
dcb "M","E","T","A","L"
dcb "C","A","U","S","E"
dcb "F","A","T","L","Y"
dcb "S","L","E","E","K"
dcb "F","O","C","U","S"
dcb "F","L","O","O","R"
dcb "O","U","T","E","R"
dcb "A","G","E","N","T"
dcb "A","L","I","V","E"
dcb "A","V","O","I","D"
dcb "E","N","T","R","Y"
dcb "U","T","T","E","R"
dcb "D","R","E","S","S"
dcb "S","M","I","L","E"
dcb "L","E","A","V","E"
dcb "O","D","D","L","Y"
dcb "Y","O","U","N","G"
dcb "U","N","C","L","E"
dcb "W","O","R","R","Y"
dcb "V","A","G","U","E"
dcb "I","M","A","G","E"
dcb "F","A","I","T","H"
dcb "M","O","N","E","Y"
dcb "J","A","P","A","N"
dcb "U","N","D","E","R"
dcb "S","T","A","F","F"
dcb "R","U","G","B","Y"
dcb "G","R","A","N","T"
dcb "A","P","P","L","Y"
dcb "V","A","L","I","D"
dcb "T","E","A","C","H"
dcb "B","U","I","L","D"
dcb "S","T","I","L","L"
dcb "D","R","A","F","T"
dcb "S","T","I","C","K"
dcb "P","E","A","C","E"
dcb "S","T","Y","L","E"
dcb "G","R","E","A","T"
dcb "A","G","O","G","O"
dcb "R","E","D","L","Y"
dcb "F","I","E","L","D"
dcb "A","L","I","N","E"
dcb "A","R","I","S","E"
dcb "L","U","N","C","H"
dcb "J","O","N","E","S"
dcb "S","L","E","E","P"
dcb "A","M","O","N","G"
dcb "S","A","D","L","Y"
dcb "I","D","E","A","L"
dcb "S","O","U","N","D"
dcb "C","O","A","C","H"
dcb "I","M","P","L","Y"
dcb "Y","O","U","T","H"
dcb "A","N","G","R","Y"
dcb "C","R","O","W","D"
dcb "E","N","J","O","Y"
dcb "S","E","C","U","S"
dcb "A","H","U","L","L"
dcb "S","E","R","V","E"
dcb "A","R","E","A","R"
dcb "E","A","R","L","Y"
dcb "S","M","O","K","E"
dcb "W","A","N","L","Y"
dcb "E","X","T","R","A"
dcb "R","A","N","G","E"
dcb "S","H","O","O","T"
dcb "C","H","A","I","R"
dcb "P","I","A","N","O"
dcb "P","I","L","O","T"
dcb "C","I","R","C","A"
dcb "P","L","O","N","K"
dcb "E","N","E","M","Y"
dcb "H","A","P","P","Y"
dcb "R","E","F","E","R"
dcb "C","L","I","M","B"
dcb "P","A","N","E","L"
dcb "N","E","W","L","Y"
dcb "A","W","F","U","L"
dcb "C","R","A","Z","Y"
dcb "G","R","A","N","D"
dcb "D","R","E","A","M"
dcb "M","O","N","T","H"
dcb "D","E","P","T","H"
dcb "S","H","A","R","P"
dcb "S","T","E","A","M"
dcb "F","O","R","T","E"
dcb "S","H","I","R","T"
dcb "F","I","R","S","T"
dcb "A","L","O","U","D"
dcb "W","R","O","N","G"
dcb "S","P","A","R","E"
dcb "C","Y","C","L","E"
dcb "J","U","D","G","E"
dcb "B","R","I","E","F"
dcb "D","R","I","V","E"
dcb "C","H","A","I","N"
dcb "A","C","C","E","L"
dcb "S","C","E","N","E"
dcb "F","I","N","A","L"
dcb "L","A","U","G","H"
dcb "C","A","T","C","H"
dcb "H","E","A","V","Y"
dcb "F","I","F","T","H"
dcb "N","O","B","L","Y"
dcb "D","I","R","T","Y"
dcb "T","W","I","C","E"
dcb "S","O","R","R","Y"
dcb "C","L","A","I","M"
dcb "A","M","I","S","S"
dcb "T","A","L","L","Y"
dcb "M","A","Y","B","E"
dcb "Q","U","E","E","N"
dcb "H","A","P","L","Y"
dcb "H","U","M","A","N"
dcb "C","L","E","A","N"
dcb "T","A","B","L","E"
dcb "O","U","G","H","T"
dcb "P","O","W","E","R"
dcb "Q","U","I","T","E"
dcb "A","N","G","E","R"
dcb "V","O","I","C","E"
dcb "S","T","O","N","E"
dcb "B","A","D","L","Y"
dcb "A","F","T","E","R"
dcb "C","O","V","E","R"
dcb "W","A","S","T","E"
dcb "R","A","I","S","E"
dcb "H","O","R","S","E"
dcb "S","H","A","L","L"
dcb "A","S","I","D","E"
dcb "A","R","G","U","E"
dcb "L","O","O","S","E"
dcb "H","E","A","R","T"
dcb "D","O","U","B","T"
dcb "P","R","I","O","R"
